---
layout: post
status: publish
published: true
title: 'Erlang Examples: Talk with Sockets'
author:
  display_name: Geoff Lane
  login: admin
  email: geoff@zorched.net
  url: http://www.zorched.net
author_login: admin
author_email: geoff@zorched.net
author_url: http://www.zorched.net
wordpress_id: 111
wordpress_url: http://www.zorched.net/?p=111
date: '2008-05-29 19:52:58 -0400'
date_gmt: '2008-05-30 01:52:58 -0400'
categories:
- Erlang
tags:
- erlang exercises
comments:
- id: 11885
  author: 'Erlang Examples: Talk with Erlang'
  author_email: ''
  author_url: http://www.zorched.net/2008/05/30/erlang-examples-talk-with-erlang/
  date: '2008-05-30 14:55:07 -0400'
  date_gmt: '2008-05-30 20:55:07 -0400'
  content: "[...] is functionally similar to Erlang Talk with Sockets. The difference
    is that it is doing Erlang-to-Erlang only communication taking advantage of [...]"
- id: 14367
  author: safari
  author_email: auf.safari@gmx.de
  author_url: ''
  date: '2008-12-19 17:22:24 -0500'
  date_gmt: '2008-12-19 23:22:24 -0500'
  content: Many thanks for your listing :-) Line 10 should end with a ';'
- id: 14370
  author: Geoff Lane
  author_email: geoff@zorched.net
  author_url: http://www.zorched.net
  date: '2008-12-19 22:14:51 -0500'
  date_gmt: '2008-12-20 04:14:51 -0500'
  content: safari, thanks for pointing out the typo. It's fixed now.
---
<p>This is part of a series on the <a href="http://www.erlang.org/course/exercises.html">Erlang Exercises</a> which is a great set of programming problems that challenge you to implement solutions to some common Erlang problems. I'm going to share some of my solutions to these problems.</p>
<h2>Erlang using UNIX sockets</h2>
Do you want to talk with a friend on another machine? Shouldn't it be nice to have a shell connected to your friend and transfer messages in between?
This can be implemented using the client/server concept with a process on each side listening to a socket for messages.</p>
<p>{% highlight erlang %}
-module(talk).
-compile(export_all).
start(LocalPort, RemotePort) ->
    ServerPid = spawn(?MODULE, start_server, [LocalPort]),
    cli(RemotePort, ServerPid).
cli(RemotePort, ServerPid) ->
    Data = clean(io:get_line('Message: ')),
    case should_stop(Data) of
        true ->
            ServerPid ! done;
        false ->
            spawn(?MODULE, send, [RemotePort, Data]),
            cli(RemotePort, ServerPid)
    end.
clean(Data) ->
    string:strip(Data, both, $\n).
should_stop(Str) ->
    0 =:= string:len(Str).
start_server(Port) ->
    case gen_tcp:listen(Port, [binary, {packet, 4},
                               {reuseaddr, true},
                               {active, true}]) of
        {ok, Listen} ->
            server(Listen);
        {error, Reason} ->
            {error, Reason}
    end.
server(Listen) ->
    case gen_tcp:accept(Listen) of
        {ok, Socket} ->
            server_loop(Socket),
            server(Listen);
        _ ->
            ok
    end.
server_loop(Socket) ->
    receive
        done ->
            gen_tcp:close(Socket),
            io:format("Server socket closed~n");
        {tcp, Socket, Bin} ->
            Str = clean(binary_to_term(Bin)),
            io:format("~p~n", [Str]),
            case should_stop(Str) of
                true ->
                    void;
                false ->
                    server_loop(Socket)
            end;
        {tcp_closed, _Socket} ->
            ok
    end.
send(Port, Data) ->
    case gen_tcp:connect("localhost", Port, [binary, {packet, 4}]) of
        {ok, Socket} ->
            gen_tcp:send(Socket, term_to_binary(Data)),
            gen_tcp:close(Socket);
        {error, Reason} ->
            {error, Reason}
    end.
{% endhighlight %}</p>
<h3>Explanation</h3>
<strong>start(LocalPort, RemotePort) -></strong>
The entry point to the talk program. This spawns a new start_server process that will handle opening a socket and listening for messages from a remote client. It then runs the command line interface and waits for data from the user to send to the remote port.</p>
<p><strong>cli(RemotePort, ServerPid) -></strong>
This is the loop that will get the data from the user and send it to the remote port.</p>
<p><strong>clean(Data)</strong>
This is a simple function that removes the newlines from the end of the data sent to the client.</p>
<p><strong>should_stop(Str)</strong>
This is a simple function that determines whether the client and server should shut down. The rule is that if it's an empty string then the processes should stop.</p>
<p><strong>start_server(Port) -></strong>
This function gets run in a process and opens a Listening socket. It then hands off to <strong>server(Listen) -></strong> to handle dealing with new incoming socket connections.</p>
<p><strong>server(Listen) -></strong>
The server gets a new Socket when an incoming connection connects to the Listening port. It then goes into a receive state and waits for an incoming connection to send it data. The receive state is handled in the <strong>server_loop</strong>.</p>
<p><strong>send(Port, Data) -></strong>
This is the client side of the application. It connects to a remote port and sends the data to that port.</p>
<p><strong>case ... of</strong>
In a number of these functions you see a common idiom:
{% highlight erlang %}
case fun() of
    SomeMatch ->
        Do something;
    AnotherMatch ->
        Do something else
end
{% endhighlight %}</p>
<p>With socket connections and many others it is common to return a tuple like <strong>{ok, Val}</strong> when the function call is successful and will return <strong>{error, Reason}</strong> or a similar tuple when there is an error in the function. The case statement is just an easy way to handle these differences.</p>
