---
layout: post
status: publish
published: true
title: 'Erlang Examples: Talk with Erlang'
author:
  display_name: Geoff Lane
  login: admin
  email: geoff@zorched.net
  url: http://www.zorched.net
author_login: admin
author_email: geoff@zorched.net
author_url: http://www.zorched.net


date: '2008-05-30 14:55:04 -0400'
date_gmt: '2008-05-30 20:55:04 -0400'
categories:
- Erlang
tags:
- erlang exercises
comments:
- id: 13264
  author: Chris B
  author_email: coldfusion78@gmail.com
  author_url: ''
  date: '2008-06-24 01:40:06 -0400'
  date_gmt: '2008-06-24 07:40:06 -0400'
  content: "My version of erlang doesn't like the case statement as you've written
    it.  only\r\n\r\ncase string:equal(\"exit!\", Message) of\r\n\r\nworks.  Is that
    a typo?"
- id: 13266
  author: Geoff Lane
  author_email: geoff@zorched.net
  author_url: http://www.zorched.net
  date: '2008-06-24 07:47:55 -0400'
  date_gmt: '2008-06-24 13:47:55 -0400'
  content: "Chris,\r\nYep, I think it was a typo - you're right it's 'of' not 'do'.
    Too much Ruby in my head probably. :)\r\nThanks for pointing that out, it's fixed
    now."
---
<p>This is part of a series on the <a href="http://www.erlang.org/course/exercises.html">Erlang Exercises</a> which is a great set of programming problems that challenge you to implement solutions to some common Erlang problems. I'm going to share some of my solutions to these problems.</p>
<h2>Implementing Talk with Distributed Erlang</h2>
Make a simple Talk program that makes it possible to chat with friends at other nodes/hosts.
<em>Hints: Your program should consist of two registered processes one for reading from the terminal and the other one for recieving messages from the other node then writing them to the terminal.</em></p>
<p>{% highlight erlang %}
-module(talk2).
-compile(export_all).
start() ->
    OtherNode = clean(io:get_line('What node? ')),
    FullNode = string:concat(OtherNode, "@localhost"),
    io:format("talking to: ~s~n", [FullNode]),
    register(receiver, spawn(?MODULE, receiver, [])),
    register(sender, spawn(?MODULE, sender, [list_to_atom(FullNode)])),
    get_input().
get_input() ->
    Message = clean(io:get_line('Talk: ')),
    case string:equal("exit!", Message) of
        true ->
            receiver ! done,
            sender ! done;
        false ->
            talk(Message),
            get_input()
    end.
talk(Message) ->
    sender ! {send, Message}.
sender(OtherNode) ->
    receive
        {send, Message} ->
            rpc:call(OtherNode, talk2, send_message, [Message]),
            sender(OtherNode);
        done ->
            void
    end.
send_message(Message) ->
    receiver ! {message, Message}.
receiver() ->
    receive
        {message, Message} ->
            io:format("~s~n", [Message]),
            receiver();
        done ->
            void
    end.
clean(Data) ->
    string:strip(Data, both, $\n).
{% endhighlight %}</p>
<h3>Explanation</h3>
This is functionally similar to <a href="http://www.zorched.net/2008/05/29/erlang-examples-talk-with-sockets/">Erlang Talk with Sockets</a>. The difference is that it is doing Erlang-to-Erlang only communication taking advantage of Erlang's distributed functionality. The Socket example could have been made to interact with any programming language that can do socket programming.</p>
<p><strong>start() -></strong>
This starts the application by asking the user for the name of the other Erlang node to talk to. Erlang nodes are started by running them with a node name <em>erl -name foo</em>. That along with host information are used to talk to a remote node.</p>
<p>So, first using <strong>io:get_line('What node? ')</strong> we ask the user then name of the other node. Then we spawn 2 processes. One to receive messages and one to send them. The interesting part here is the use of the <strong>register</strong> function. It takes an atom and a process id. The register function basically binds a pid to an atom and creates a globally accessible pid. It just simplifies the code a bit so you don't have to pass those pids to all of the other functions.</p>
<p><strong>get_input() -></strong>
This is the main user input loop. It asks the user for a message and then sends that message to the sender process. <strong>io:get_line('Talk: ')</strong> waits for user input before it returns, so only after a user types something and hits return will this function continue. If the <strong>case string:equal("exit!", Message) do</strong> evaluates to true, then the sender and receiver processes are told to exit and the program ends naturally. Otherwise the message is sent.</p>
<p><strong>talk(Message) -></strong>
This function sends the message to the sender process.</p>
<p><strong>sender(OtherNode) -></strong>
This is the function that is running as the sender process. It is initialized with the remote <strong>OtherNode</strong> information. It receives messages and forwards them to the OtherNode. The <strong>rpc:call(OtherNode, talk2, send_message, [Message]),</strong> call invokes the <strong>send_message</strong> function running on the remote node.</p>
<p><strong>send_message(Message) -></strong>
This is really an RPC helper method that passes messages it get to the process running the <strong>receive</strong> function.</p>
<p><strong>receiver() -></strong>
The receiver is the function that is being run as the receiver process. It listens for messages and when it gets one it prints it out to the user.</p>
<h2>Thoughts</h2>
The other interesting way to solve this would be to actually spawn processes remotely on the other node. The code can then have a reference to a pid running on the remote node and can pass messages to it directly without using the RPC approach. This would allow one side of the conversation to initiate it without the other knowing about it.</p>
<p>
{% highlight erlang %}
register(remote_receiver, spawn(OtherNode, talk2, receiver, [])),
remote_receiver ! "Hello".
{% endhighlight %}</p>
<p>An exercise I'll leave up to you!</p>
